prototype(Carbon.BackendDocument:Editable.Fragment.Options) < prototype(Neos.Fusion:Component) {
    options = ${null}
    optionGroups = ${null}
    value = ${null}

    hasOptions = ${Carbon.Array.check(this.options)}
    hasGroup = ${Carbon.Array.check(this.optionGroups)}

    @if.hasOptions = ${this.hasOptions || this.hasGroup}

    renderer = afx`
        <Neos.Fusion:Loop @if.set={props.hasOptions} items={props.options}>
            <option value={itemKey} selected={props.value == itemKey}>{item}</option>
        </Neos.Fusion:Loop>
        <Neos.Fusion:Loop @if.set={props.hasGroup} items={props.optionGroups}>
            <optgroup label={item.label} disabled={item.disabled}>
                <Carbon.BackendDocument:Editable.Fragment.Options
                    options={item.options}
                    optionGroups={item.optionGroups}
                    value={props.value}
                />
            </optgroup>
        </Neos.Fusion:Loop>
    `
}
